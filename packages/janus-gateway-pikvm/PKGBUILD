#_commit=7b784c40ac9e8d1fa07c403c013c8559692b7e1c
pkgname=janus-gateway-pikvm
pkgver=0.11.5
pkgrel=2
pkgdesc="An open source, general purpose, WebRTC server (Pi-KVM build)"
url='https://janus.conf.meetecho.com'
license=(GPL3)
arch=(i686 x86_64 armv6h armv7h)
depends=(
	glib2
	jansson
	libconfig
	libwebsockets
	libnice
	libsrtp
	openssl
)
makedepends=(
	cmake
	gcc
	pkg-config
	gengetopt
	autoconf
)
conflicts=(
	janus-gateway
	janus-gateway-git
)
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/meetecho/janus-gateway/archive/v$pkgver.tar.gz"
#	"$pkgname-$_commit.tar.gz::https://github.com/meetecho/janus-gateway/archive/$_commit.tar.gz"
	"https://webrtc.github.io/adapter/adapter-latest.js"
)
md5sums=(SKIP SKIP)


build() {
	cd "janus-gateway-$pkgver"
#	cd "janus-gateway-$_commit"
	./autogen.sh
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-docs \
		--disable-data-channels \
		--disable-turn-rest-api \
		--disable-all-plugins \
		--disable-all-loggers \
		--disable-all-transports \
		--enable-websockets \
		--disable-sample-event-handler \
		--disable-websockets-event-handler \
		--disable-gelf-event-handler
	make $MAKEFLAGS
}

package() {
	cd "janus-gateway-$pkgver"
#	cd "janus-gateway-$_commit"
	make DESTDIR="$pkgdir" install
	sed -i -e 's|^#include "refcount.h"$|#include "../refcount.h"|g' "$pkgdir/usr/include/janus/plugins/plugin.h"
	sed -i -e 's|^function Janus(|export function Janus(|g' "$pkgdir/usr/share/janus/javascript/janus.js"
	sed -i '1s|^|import "./adapter.js"\n|' "$pkgdir/usr/share/janus/javascript/janus.js"
	cp ../adapter-latest.js "$pkgdir/usr/share/janus/javascript/adapter.js"
}
